repositories {
    mavenCentral()
}

dependencies {
    implementation project(':checker-qual')

    // This is used by org.checkerframework.javacutil.TypesUtils.isImmutableTypeInJdk.
    // https://mvnrepository.com/artifact/org.plumelib/plume-util
    implementation 'org.plumelib:plume-util:1.4.1'

    // External dependencies:
    // If you add an external dependency, you must shadow its packages.
    // See the comment in ../build.gradle in the shadowJar block.

    /// TODO: The below is temporary until the `removeDuplicates` routine is moved into plume-lib.
    // TODO: it's a bug that annotatedlib:guava requires the error_prone_annotations dependency.
    // Update the next two version numbers in tandem.  Get the Error Prone version from the "compile
    // dependencies" section of https://mvnrepository.com/artifact/com.google.guava/guava/28.2-jre .
    // (It isn't at https://mvnrepository.com/artifact/org.checkerframework.annotatedlib/guava/28.2-jre, which is the bug.)
    implementation 'com.google.errorprone:error_prone_annotations:2.5.1'
    implementation ('org.checkerframework.annotatedlib:guava:30.1-jre') {
        // So long as Guava only uses annotations from checker-qual, excluding it should not cause problems.
        exclude group: 'org.checkerframework'
    }
}

apply from: rootProject.file("gradle-mvn-push.gradle")

final javacUtilPom(publication) {
    sharedPublicationConfiguration(publication)
    publication.from components.java

    publication.pom {
        name = 'Javacutil'
        description = 'javacutil contains utility classes for the javac compiler.'
        licenses {
            license {
                name = 'GNU General Public License, version 2 (GPL2), with the classpath exception'
                url = 'http://www.gnu.org/software/classpath/license.html'
                distribution = 'repo'
            }
        }
    }
}
publishing {
    publications {
        javacUtil(MavenPublication) {
            javacUtilPom it
        }
    }
}
signing {
    sign publishing.publications.javacUtil
}
